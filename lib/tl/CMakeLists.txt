# @formatter:off
cmake_minimum_required(VERSION 3.1)

project(tloptional)

option(TLOPTIONAL_ENABLE_TESTS "Enable tests." OFF)
option(TLOPTIONAL_ENABLE_DOCS "Enable documentation." OFF)

add_library(tloptional INTERFACE)
target_sources(tloptional INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/include/tl/optional.hpp)
target_include_directories(tloptional INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/include)

if(TLOPTIONAL_ENABLE_TESTS)
  # Prepare "Catch" library for other executables
  set(CATCH_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/tests)
  add_library(Catch INTERFACE)
  target_include_directories(Catch INTERFACE ${CATCH_INCLUDE_DIR})

  # Make test executable
  set(TEST_SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/tests/main.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/noexcept.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/make_optional.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/in_place.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/relops.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/observers.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/extensions.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/emplace.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/constexpr.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/constructors.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/assignment.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/issues.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/bases.cpp
                   ${CMAKE_CURRENT_SOURCE_DIR}/tests/nullopt.cpp)

  add_executable(tests ${TEST_SOURCES})

  target_link_libraries(tests Catch optional)

  set(CXXSTD 14 CACHE STRING "C++ standard to use, default C++14")
  set_property(TARGET tests PROPERTY CXX_STANDARD ${CXXSTD})
endif()

if(TLOPTIONAL_ENABLE_DOCS)
  find_package(standardese) # find standardese after installation

  # generates a custom target that will run standardese to generate the documentation
  if(standardese_FOUND)
    standardese_generate(optional
                         INCLUDE_DIRECTORY tl
                         CONFIG ${CMAKE_CURRENT_SOURCE_DIR}/standardese.config
                         INPUT tl/optional.hpp)
  endif()
endif()
